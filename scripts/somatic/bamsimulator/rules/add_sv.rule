#! /usr/bin/env python
# tabstop=4 expandtab


rule add_sv:
    input:
        snvs_indels_added_bam = "bam/snvs_indels_added.bam",
        varfile = "bed/random_sSV.bed",
        cnvfile = "bed/cnvfile.bed.gz",
        reference_genome = reference['genome']
    params:
        procs = params['procs'],
        seed = params['seed'],
        tmpdir = params['tmpdir'],
    output:
        unsorted_svs_added_bam = "bam/unsorted_snvs_indels_svs_added.bam",
        addsv_logs = directory("addsv_logs_unsorted_snvs_indels_svs_added.bam")
    shell:
        """
        /usr/local/bamsurgeon/bin/addsv.py \
                --svfrac 0.1 --procs {params.procs} \
                --varfile {input.varfile} \
                --bamfile  {input.snvs_indels_added_bam} \
                --reference {input.reference_genome} \
                --outbam {output.unsorted_svs_added_bam} \
                --maxlibsize 1000 --cnvfile {input.cnvfile} \
                --tagreads --seed {params.seed} --aligner mem \
                --tmpdir {params.tmpdir}
        """

rule makevcf_sv:
    input:
        addsv_logs = "addsv_logs_unsorted_snvs_indels_svs_added.bam",
        reference_genome = reference['genome'],
        fai_index = reference['genome_fai']
    output:
        synthetic_svs = "vcf/synthetic_svs.vcf"
    shell:
        """
        /usr/local/bamsurgeon/scripts/makevcf_sv.py \
            -l {input.addsv_logs} \
            -r {input.reference_genome} \
        | bedtools sort -header -faidx {input.fai_index} > {output.synthetic_svs}
        """


rule samtools_sort_index:
    input:
        reference_genome = reference['genome'],
        svs_unsorted_bam = "bam/unsorted_snvs_indels_svs_added.bam"
    params:
        memory = '4G'
    output:
        svs_added_bam = "bam/snvs_indels_svs_added.bam"
    shell:
        """
        samtools sort -m {params.memory} \
            --reference {input.reference_genome} \
            -o {output.svs_added_bam} {input.svs_unsorted_bam} &&
        samtools index {output.svs_added_bam}
        """